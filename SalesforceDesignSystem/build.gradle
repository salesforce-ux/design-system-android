apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = '3.0.2'
group = 'com.salesforce.ux'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 3
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                name 'SalesforceDesignSystem'
                url 'https://github.com/salesforce-ux/design-system-android'

                packaging 'aar'
                version version

                licenses {
                    license {
                        name 'BSD 3-clause'
                        url 'https://github.com/salesforce-ux/design-system-android/blob/master/LICENSE.txt'
                    }
                }

                developers {
                    developer {
                        name 'Salesforce UX'
                    }
                }

                scm {
                    connection 'https://github.com/salesforce-ux/design-system-android.git'
                    developerConnection 'https://github.com/salesforce-ux/design-system-android.git'
                    url 'https://github.com/salesforce-ux/design-system-android'
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'salesforce-ux'
        name = 'SalesforceDesignSystem'
        userOrg = 'salesforce-ux'
        licenses = ['BSD 3-Clause']
        vcsUrl = 'https://github.com/salesforce-ux/design-system-android.git'
        labels = ['android', 'salesforce', 'lightning', 'slds']
        version {
            name = version
            released = new Date()
        }
    }
}
